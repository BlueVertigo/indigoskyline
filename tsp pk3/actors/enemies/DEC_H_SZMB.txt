//===========================================================================
// ShotgunGuy Replacements
//===========================================================================

ACTOR ShotgunguySkillSpawner : MonsterSkillSpawner replaces ShotgunGuy
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") == 5,"LSSpawn")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") > 2,"Spawn2")
    TNT1 A 0 A_SpawnItemEx("ShotgunguySpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  Spawn2:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("ShotgunguySpawnerHard",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  LSSpawn:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("LSShotgunguySpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR ShotgunguySpawner : RandomSpawner 
{
  DropItem "ShotgunGuyV0", 255,100
  DropItem "LSJudgmentZombie", 255, 15
}

ACTOR ShotgunguySpawnerHard : RandomSpawner 
{
  DropItem "HardShotgunGuyV0", 255,100
  DropItem "HardJudgmentZombie", 255, 50
}

ACTOR LSShotgunguySpawner : RandomSpawner 
{
  DropItem "LSZombieScientistV1", 255, 150
  DropItem "LSZombieScientistV2", 255, 150
  DropItem "LSZombieScientistV3", 255, 150
  DropItem "LSZombieScientistV4", 255, 150
  DropItem "LSFemaleScientistV1", 255, 150
  DropItem "LSFemaleScientistV2", 255, 150
  DropItem "LSFemaleScientistV3", 255, 150
  DropItem "LSFemaleScientistV4", 255, 150
  DropItem "LSHazmatZombie", 255, 150
  DropItem "LSShotgunGuyV0", 255,100
  DropItem "LSJudgmentZombie", 255, 50
}

ACTOR BaseShotgunGuy : BaseZombieHitscanner {}

//===========================================================================
// Shotgun Zombie (Vanilla)
//
// Sprites by idsoftware
//===========================================================================

ACTOR BaseShotgunGuyV0 : BaseShotgunGuy
{
  Health 30
  Radius 18
  Height 56
  Mass 100
  Speed 3
  PainChance 170
  SeeSound "zombie/sight"
  PainSound "zombie/pain"
  DeathSound "zombie/death"
  ActiveSound "zombie/idle"
  AttackSound "zekeshotgun/fire"
  PainChance "Melee", 235
  PainChance "GasShellDamage", 17
  Obituary "%o was blasted by a shotgun-wielding zombie."
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 127
  DropItem BuckshotShellThrown 127
  DropItem BuckshotShellThrown 127
  DropItem BuckshotShellThrown 255
  DropItem BuckshotShellThrown 255
  DropItem ShotgunPickup 255
  //DropItem DropBuckshotShellSpawner 255 These guys are the pretty 4-shell things
  States
  {
  Spawn2: 
    SPOS A 10 A_Look 
    Loop
  Missile: 
    SPOS E 10 A_FaceTarget 
    SPOS F 10 Bright A_CustomBulletAttack(10,2,9,1)
    SPOS E 10 A_PlaySound("zekeshotgun/pump")
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain:
    SPOS G 3
    SPOS G 3 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain.Melee:
    SPOS G 6
    SPOS G 8 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Death:
    SPOS H 5
    SPOS I 5 A_Scream
    SPOS J 5 A_NoBlocking
    SPOS K 5
    SPOS L -1
    Stop
  XDeath:
    SPOS M 5
    SPOS N 5 A_XScream
    SPOS O 5 A_NoBlocking
    SPOS PQRST 5
    SPOS U -1
    Stop
  Raise:
    SPOS L 5
    SPOS KJIH 5
    TNT1 A 0 A_Jump (256,"See2")
    Goto See    
  }
}

ACTOR ShotgunGuyV0 : BaseShotgunGuyV0
{
  States
  {
  See2:
    SPOS A 1 A_Chase
    SPOS AA 1 A_Chase("","")
    SPOS A 1 A_Chase
    SPOS AA 1 A_Chase("","")
    SPOS B 1 A_Chase
    SPOS BB 1 A_Chase("","")
    SPOS B 1 A_Chase
    SPOS BB 1 A_Chase("","")
    SPOS C 1 A_Chase
    SPOS CC 1 A_Chase("","")
    SPOS C 1 A_Chase
    SPOS CC 1 A_Chase("","")
    SPOS D 1 A_Chase
    SPOS DD 1 A_Chase("","")
    SPOS D 1 A_Chase
    SPOS DD 1 A_Chase("","")
    Loop 
  }
}

ACTOR HardShotgunGuyV0 : BaseShotgunGuyV0
{
  States
  {
  See2:
    SPOS AA 1 A_Chase
    SPOS A 1 A_Chase("","")
    SPOS A 1 A_Chase
    SPOS AA 1 A_Chase("","")
    SPOS BB 1 A_Chase
    SPOS B 1 A_Chase("","")
    SPOS B 1 A_Chase
    SPOS BB 1 A_Chase("","")
    SPOS CC 1 A_Chase
    SPOS C 1 A_Chase("","")
    SPOS C 1 A_Chase
    SPOS CC 1 A_Chase("","")
    SPOS DD 1 A_Chase
    SPOS D 1 A_Chase("","")
    SPOS D 1 A_Chase
    SPOS DD 1 A_Chase("","")
    Loop 
  }
}

ACTOR LSShotgunGuyV0 : HardShotgunGuyV0
{
  Painchance 100
  PainChance "Melee", 150
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 50
  DropItem BuckshotShellThrown 50
  DropItem ShotgunPickup 100
  DropItem TSPArmorBonusDrop 150
  States
  {
  Missile: 
    SPOS E 10 A_FaceTarget 
    SPOS F 10 Bright A_CustomBulletAttack(10,2,9,2)
    SPOS E 10 A_PlaySound("zekeshotgun/pump")
    TNT1 A 0 A_Jump (256,"See2")
    Goto See 
  }
}