//===========================================================================
// Cacodemon Replacements
//===========================================================================

ACTOR CacodemonSkillSpawner : MonsterSkillSpawner replaces Cacodemon
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") > 3,"Spawn2")
    TNT1 A 0 A_SpawnItemEx("CacodemonSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERAMBUSHFLAG)
    Stop
  Spawn2:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("CacodemonSpawnerHard",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERAMBUSHFLAG)
    Stop
  }
}

ACTOR CacodemonSpawner : RandomSpawner
{
  DropItem "CacodemonV0"
}

ACTOR CacodemonSpawnerHard : RandomSpawner
{
  DropItem "HardCacodemonV0"
}

ACTOR BaseCacodemon : BaseFloater
{
  BloodColor "00 00 FF"
}

ACTOR BaseCacodemonV0 : BaseCacodemon
{
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 3
  PainChance 128
  PainChance "Melee", 205
  PainChance "GasShellDamage", 13
  SeeSound "caco/sight"
  PainSound "caco/pain"
  DeathSound "caco/death"
  ActiveSound "caco/active"
  Obituary "$OB_CACO"
  HitObituary "$OB_CACOHIT"
  States
  {
  Spawn:
    "####" A 0 A_JumpIfInventory("AGO_Spawn",1,"Spawn2")
    "####" A 0
    "####" A 0 ACS_NamedExecuteWithResult("MonsterEnter")
    "####" A 0 A_GiveInventory("AGO_Spawn",1)
	"####" "#" 0 A_Jump (256,"Spawn2")
    Goto Spawn2
  Spawn2:
    HEAD A 10 A_Look
    Loop
  See2:
    "####" A 0 A_JumpIfInventory("Diff3",1,"See2Hard")
    HEAD A 1 A_Chase
    HEAD AA 1 A_Chase("","")
    Loop
  See2Hard:
    HEAD A 1 A_Chase
    Loop
  Melee:
    Goto Missile
  Melee2:
	"####" "#" 0 A_JumpIfInventory("Diff5",1,"Melee2LS")
	"####" "#" 0 A_JumpIfInventory("Diff3",1,"Melee2Hard")
    "####" "#" 0 A_CustomComboAttack("CacodemonBall", 32, 10 * random(1, 6))
    Goto MissileFinish
  Melee2Hard:
    "####" "#" 0 A_CustomComboAttack("FastCacodemonBall", 32, 10 * random(1, 6))
    Goto MissileFinish
  Melee2LS:
    "####" "#" 0 A_CustomComboAttack("FastCacodemonBallLS", 32, 10 * random(1, 6))
    Goto MissileFinish
  Missile:
    HEAD BC 5 A_FaceTarget
	"####" "#" 0 A_JumpIfCloser(256,"Melee2")
    "####" "#" 0 A_JumpIf(ACS_NamedExecuteWithResult("CacoMissile"),"MissileFinish")
    Goto Melee2
  MissileFinish:
    HEAD D 5 Bright
    "####" "#" 0 A_Jump (256,"See2")
    Goto See
  Pain:
    HEAD E 3
    HEAD E 3 A_Pain
    HEAD F 6
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain.Melee:
    HEAD E 5
    HEAD E 3 A_Pain
    HEAD F 7
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Death:
    HEAD G 8
    HEAD H 8 A_Scream
    HEAD IJ 8
    HEAD K 8 A_NoBlocking
    HEAD L -1 A_SetFloorClip
    Stop
  Raise:
    HEAD L 8 A_UnSetFloorClip
    HEAD KJIHG 8
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  }
}

ACTOR FastCacodemonBall : CacodemonBall { Speed 20 }
ACTOR FastCacodemonBallLS : CacodemonBall { Speed 15 }

ACTOR CacodemonV0 : BaseCacodemonV0 {}

ACTOR HardCacodemonV0 : BaseCacodemonV0 {}