//===========================================================================
// Cyberdemon Replacements
//===========================================================================

ACTOR CyberdemonSkillSpawner : MonsterSkillSpawner replaces Cyberdemon
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") > 3,"Spawn2")
    TNT1 A 0 A_SpawnItemEx("CyberdemonSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERAMBUSHFLAG)
    Stop
  Spawn2:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("CyberdemonSpawnerHard",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERAMBUSHFLAG)
    Stop
  }
}

ACTOR CyberdemonSpawner : RandomSpawner
{
  DropItem "CyberdemonV0"
}

ACTOR CyberdemonSpawnerHard : RandomSpawner
{
  DropItem "HardCyberdemonV0"
}

ACTOR BaseCyberdemon : BaseBigBoss {}

ACTOR BaseCyberdemonV0 : BaseCyberdemon
{
  Health 4000
  Radius 40
  Height 110
  Mass 1000
  Speed 6
  PainChance 20
  PainChance "Melee", 40
  MinMissileChance 160
  SeeSound "cyber/sight"
  PainSound "cyber/pain"
  DeathSound "cyber/death"
  ActiveSound "cyber/active"
  Obituary "$OB_CYBORG"
  States
  {
  Spawn:
    CYBR AB 10 A_Look
    Loop
  Missile:
    CYBR E 6 A_FaceTarget
    CYBR F 12 A_CyberAttack
    CYBR E 12 A_FaceTarget
    CYBR F 12 A_CyberAttack
    CYBR E 12 A_FaceTarget
    CYBR F 12 A_CyberAttack
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain:
    CYBR G 10 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain.Melee:
    CYBR G 5
    CYBR G 15 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Death:
    CYBR H 10
    CYBR I 10 A_Scream
    CYBR JKL 10
    CYBR M 10 A_NoBlocking
    CYBR NO 10
    CYBR P 30
    CYBR P -1 A_BossDeath
    Stop
  }
}

ACTOR CyberdemonV0 : BaseCyberdemonV0
{
  States
  {
  See2:
    CYBR A 0 A_Hoof
    CYBR A 1 A_Chase
    CYBR AA 1 A_Chase("","")
    CYBR A 1 A_Chase
    CYBR AA 1 A_Chase("","")
    CYBR B 1 A_Chase
    CYBR BB 1 A_Chase("","")
    CYBR B 1 A_Chase
    CYBR BB 1 A_Chase("","")
    CYBR C 1 A_Chase
    CYBR CC 1 A_Chase("","")
    CYBR C 1 A_Chase
    CYBR CC 1 A_Chase("","")
    CYBR D 0 A_Metal
    CYBR D 1 A_Chase
    CYBR DD 1 A_Chase("","")
    CYBR D 1 A_Chase
    CYBR DD 1 A_Chase("","")
    Loop
  }
}

ACTOR HardCyberdemonV0 : BaseCyberdemonV0
{
  States
  {
  See2:
    CYBR A 0 A_Hoof
    CYBR AAAAAABBBBBBCCCCCC 1 A_Chase
    CYBR D 0 A_Metal
    CYBR DDDDDD 1 A_Chase
    Loop
  }
}