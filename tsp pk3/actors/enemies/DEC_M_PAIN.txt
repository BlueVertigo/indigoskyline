//===========================================================================
// PainElemental Replacements
//===========================================================================

ACTOR PainElementalSkillSpawner : MonsterSkillSpawner replaces PainElemental
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") > 3,"Spawn2")
    TNT1 A 0 A_SpawnItemEx("PainElementalSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  Spawn2:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("PainElementalSpawnerHard",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR PainElementalSpawner : RandomSpawner
{
  DropItem "PainElementalV0"
}

ACTOR PainElementalSpawnerHard : RandomSpawner
{
  DropItem "HardPainElementalV0"
}

ACTOR BasePainElemental : BaseFloater {}

ACTOR BasePainElementalV0 : BasePainElemental
{
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 3
  PainChance 128
  PainChance "Melee", 205
  PainChance "GasShellDamage", 13
  SeeSound "pain/sight"
  PainSound "pain/pain"
  DeathSound "pain/death"
  ActiveSound "pain/active"
  States
  {
  Spawn:
    PAIN A 10 A_Look
    Loop
  Missile:
    PAIN D 5 A_FaceTarget
    PAIN E 5 A_FaceTarget
    PAIN F 4 Bright A_FaceTarget
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetToasterCVAR") == 1,"MissileToaster1")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetToasterCVAR") > 1,"MissileToaster2")
    PAIN F 1 Bright A_PainAttack("TSPRictusToaster0")
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  MissileToaster1:
    PAIN F 1 Bright A_PainAttack("TSPRictusToaster1")
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  MissileToaster2:
    PAIN F 1 Bright A_PainAttack("TSPRictusToaster2")
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain:
    PAIN G 6
    PAIN G 6 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Pain.Melee:
    PAIN G 5
    PAIN G 10 A_Pain
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  Death:
    PAIN H 8 Bright
    PAIN I 8 Bright A_Scream
    PAIN JK 8 Bright
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetToasterCVAR") == 1,"DeathToaster1")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetToasterCVAR") > 1,"DeathToaster2")
    PAIN L 8 Bright A_PainDie("TSPRictusToaster0")
    PAIN M 8 Bright
    Stop
  DeathToaster1:
    PAIN L 8 Bright A_PainDie("TSPRictusToaster1")
    PAIN M 8 Bright
    Stop
  DeathToaster2:
    PAIN L 8 Bright A_PainDie("TSPRictusToaster2")
    PAIN M 8 Bright
    Stop
  Raise:
    PAIN MLKJIH 8
    TNT1 A 0 A_Jump (256,"See2")
    Goto See
  }
}

ACTOR PainElementalV0 : BasePainElementalV0
{
  States
  {
  See2:
    PAIN A 1 A_Chase
    PAIN AA 1 A_Chase("","")
    PAIN A 1 A_Chase
    PAIN AA 1 A_Chase("","")
    PAIN B 1 A_Chase
    PAIN BB 1 A_Chase("","")
    PAIN B 1 A_Chase
    PAIN BB 1 A_Chase("","")
    PAIN C 1 A_Chase
    PAIN CC 1 A_Chase("","")
    PAIN C 1 A_Chase
    PAIN CC 1 A_Chase("","")
    Loop
  }
}

ACTOR HardPainElementalV0 : BasePainElementalV0
{
  States
  {
  See2:
    PAIN AAAAAABBBBBBCCCCCC 1 A_Chase
    Loop
  }
}