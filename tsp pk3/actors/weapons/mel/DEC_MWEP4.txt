//===========================================================================
//
// Kurdd & Schaumers AB-50
//
// by Marty Kirra
// Sprites by Mike12, zrrion the insect, and Marty Kirra
//
//===========================================================================
ACTOR MelAmmoBox50 : Weapon
{
	Weapon.Kickback 100
	Weapon.AmmoType	"AmmoBoxMag"
	Weapon.AmmoGive	0
	Weapon.AmmoUse	1
	Weapon.AmmoType2 "AmmoBoxAmmo"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2	0
	Weapon.BobStyle Alpha
	Weapon.BobSpeed 2.0
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 0.4
	Inventory.PickupMessage "[Kurdd & Schaumers AB-50] [Class: Sub-Machine Gun, Slot 4]"
	Obituary "%o was riddled with bullets thanks to %k's AB-50."
	+Weapon.AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	Tag "K&S AB-50"
   States
   {
   Spawn:
	  ABXP A -1
	  Stop
   Ready:
      TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      ABXG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      LOOP
   Deselect:
      TNT1 A 0 A_PlaySoundEx("AmmoBox/Down", "SoundSlot5")
	  ABXD ABCDE 1
	  TNT1 A 6
   DeselectLoop:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
    TNT1 A 1 A_Lower
    Loop
   Select:
	  TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
      TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
	  TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 141)
	  TNT1 A 0 A_PlaySoundEx("AmmoBox/Up", "SoundSlot5")
      TNT1 A 4
	  ABXU ABCDE 1
   SelectLoop:
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
	  ABXG A 1 A_Raise
      Loop
   Fire:
	  TNT1 A 0 A_JumpIfNoAmmo("AutoEmpty")
	  TNT1 A 0 A_GiveInventory("AB50ShotsFiredPewPew", 1)
	  ABXG A 0 BRIGHT
      ABXG A 0 A_GunFlash
	  ABXG A 0 A_AlertMonsters
	  ABXG A 0 A_SetPitch(pitch - .3)
	  ABXG A 0 A_SetAngle(angle + (frandom(-0.2,0.2)))
      ABXG A 0 A_PlayWeaponSound("AmmoBox/fire")
    Fire0:
      ASTF A 0 BRIGHT A_FireBullets(5,1,-1,9,"919mBulletPuff",1)
	  ASTF A 0 A_Jump(64,"AFire4")
      ASTF A 0 A_Jump(64,"AFire3")
	  ASTF A 0 A_Jump(88,"Afire2")
	AFire1:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
	  ASTF B 0 A_Light2
      ABXF A 1 BRIGHT A_Light1
	  ASTF B 0 A_Light0
      Goto FireEnd
	AFire2:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
	  ASTF C 0 A_Light2
      ABXF B 1 BRIGHT A_Light1
	  ASTF C 0 A_Light0
      Goto FireEnd
	AFire3:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
	  ASTF D 0 A_Light2
      ABXF C 1 BRIGHT A_Light1
	  ASTF D 0 A_Light0
	  Goto FireEnd
	AFire4:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
	  ASTF D 0 A_Light2
      ABXF D 1 BRIGHT A_Light1
	  ASTF D 0 A_Light0
	FireEnd:
	  ABXF E 1
	  ABXF F 1
	  ABXF G 2 A_Refire
	  ABXF FE 1 
	  ABXG A 1
	  Goto Ready
	AutoEmpty:
	  ABXG A 5 A_PlayWeaponSound("919m/dry")
	  Goto Reload 
	Reload:
	  ABXG A 0 A_JumpIfInventory("AmmoBoxMag", 50, "Ready")
	  ABXG A 0 A_JumpIfInventory("AmmoBoxAmmo", 1, "ReloadStart")
	  ABXG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	ReloadStart:
	  TNT1 A 0 A_JumpIfInventory("AB50ShotsFiredPewPew", 50, "GiveCockingAnimation")//lewd
	  Goto ReloadCont
	GiveCockingAnimation:  
	  TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
	ReloadCont:
	  ABXG A 5 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
	  ABXR AUBC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
	  ABXG A 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
	  ABXR D 1 A_PlayWeaponSound("AmmoBox/Out")
	  ABXR EFGHIIIIIIIIIIIIIIIIII 1
      Goto ReloadWork
	ReloadWork:
	  MPTR I 0 A_TakeInventory("AmmoBoxAmmo",1)
	  MPTR I 0 A_GiveInventory("AmmoBoxMag",1)
	  MPTR I 0 A_JumpIfInventory("AmmoBoxMag",50,"ReloadFinish")
	  MPTR I 0 A_JumpifInventory("AmmoBoxAmmo",1,"ReloadWork")
	  Goto ReloadFinish
	ReloadFinish:
	  ABXR J 1 A_PlayWeaponSound("AmmoBox/In")
	  ABXR KLMNOP 1 
	  ABXR PSTCBUA 1
	  ABXG A 2
	  TNT1 A 0 A_TakeInventory("AB50ShotsFiredPewPew", 99)
	  TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
	  Goto Ready
	Rechamber:
	  ABXC C 0 A_PlaySound("ammobox/pull", 6)
	  ABXC ABCC 1
	  ABXC DEFG 1
	  ABXG A 3
	  TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
	  Goto Ready
	SPMelee:
	  ABXG A 1
	  ABXD ABCDE 1
	SPMeleeStart:
	  TNT1 A 5
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  TNT1 A 0 A_PlaySound("*melee", 6)
	  ABXM ABC 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
	  ABXM D 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeBerserk:
	  ABXM D 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeA: 
	  ABXM EFGHI 1
	  TNT1 A 8
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  ABXU ABCDE 1
	  Goto Ready
   }
}

ACTOR AB50ShotsFiredPewPew : Inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 50
}

ACTOR AmmoBoxMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 50
}



//===========================================================================
//
// Kurdd & Schaumers AB-50 (Magazine Version)
// Made for the Lone Survivor gameplay type
//
// by Marty Kirra
// Sprites by Mike12, zrrion the insect, and Marty Kirra
//
//===========================================================================
ACTOR MelAmmoBox50LS : MelAmmoBox50
{
	Weapon.AmmoType2 "AmmoBoxAmmoMag"
	+WEAPON.CHEATNOTWEAPON
   States
   {
 	AutoEmpty:
	  ABXG A 5 A_PlayWeaponSound("919m/dry")
	  Goto Reload 
	Reload:
	  ABXG A 0 A_JumpIfInventory("AmmoBoxMag", 50, "Ready")
	  ABXG A 0 A_JumpIfInventory("AmmoBoxAmmoMag", 1, "ReloadStart")
	  ABXG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	ReloadStart:
	  TNT1 A 0 A_JumpIfInventory("AB50ShotsFiredPewPew", 50, "GiveCockingAnimation")//lewd
	  Goto ReloadCont
	GiveCockingAnimation:  
	  TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
	ReloadCont:
	  ABXG A 5 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
	  ABXR AUBC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
	  ABXG A 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
	  ABXR D 1 A_PlayWeaponSound("AmmoBox/Out")
	  ABXR EFGHIIIIIIIIIIIIIIIIII 1
      Goto ReloadWork
	ReloadWork:
	  MPTR I 0 A_TakeInventory("AmmoBoxAmmoMag",1)
	  MPTR I 0 A_GiveInventory("AmmoBoxMag",50)
	  Goto ReloadFinish
	ReloadFinish:
	  ABXR J 1 A_PlayWeaponSound("AmmoBox/In")
	  ABXR KLMNOP 1 
	  ABXR PSTCBUA 1
	  ABXG A 2
	  TNT1 A 0 A_TakeInventory("AB50ShotsFiredPewPew", 99)
	  TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
	  Goto Ready
	Rechamber:
	  ABXC C 0 A_PlaySound("ammobox/pull", 6)
	  ABXC ABCC 1
	  ABXC DEFG 1
	  ABXG A 3
	  TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
	  Goto Ready
	SPMelee:
	  ABXG A 1
	  ABXD ABCDE 1
	SPMeleeStart:
	  TNT1 A 5
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  TNT1 A 0 A_PlaySound("*melee", 6)
	  ABXM ABC 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
	  ABXM D 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeBerserk:
	  ABXM D 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeA: 
	  ABXM EFGHI 1
	  TNT1 A 15
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  ABXU ABCDE 1
	  Goto Ready
   }
}

//===========================================================================
//
// Maschinenpistole 40 (MP 40)
//
// by Marty Kirra
// Sprites by Marty Kirra, Valve
//
//===========================================================================
ACTOR MelMipFowty : Weapon
{
	Weapon.Kickback 100
	Weapon.AmmoType	"MP40Mag"
	Weapon.AmmoGive	0
	Weapon.AmmoUse	1
	Weapon.AmmoType2 "TSPPistolAmmo"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2	0
	Weapon.BobStyle Alpha
	Weapon.BobSpeed 1.5
	Weapon.BobRangeY 0.4
	Weapon.BobRangeX 0.6
	Inventory.PickUpSound "mp40/pickup"
	Inventory.PickupMessage "[Maschinenpistole 40] [Class: Sub-Machine Gun, Slot 4]"
	Obituary "%o schoss %k mit einem Relikt der Vergangenheit. Sieg Heil!"
	+Weapon.AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NoAlert
	Tag "Maschinepistole 40"
   States
   {
   Spawn:
	  MP4P A -1
	  Stop
   Ready:
      TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      MP4G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      LOOP
   Deselect:
      TNT1 A 0 A_PlaySoundEx("mipfowty/down", "SoundSlot5")
	  MP4D ABCD 1
	  TNT1 A 6
   DeselectLoop:
	  TNT1 A 1 A_Lower
	  Loop
   Select:
	  TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
      TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
	  TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 149)
      TNT1 A 0 A_PlaySoundEx("mipfowty/Up", "SoundSlot5")
      TNT1 A 6
	  MP4U ABCD 1
   SelectLoop:
	  MP4G A 1 A_Raise
      Loop
   Fire:
	  TNT1 A 0 A_JumpIfNoAmmo("Empty")
	  MP4G A 0 BRIGHT
      MP4G A 0 A_GunFlash
	  MP4G A 0 A_AlertMonsters
	  MP4G A 0 A_SetPitch(pitch - .4)
	  MP4G A 0 A_SetAngle(angle + (frandom(-0.4,0.4)))
      MP4G A 0 A_PlayWeaponSound("mipfowty/fire")
      MP4F A 0 BRIGHT A_FireBullets(3,6,-1,10,"919mBulletPuff",1)
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
	  ASTF B 0 A_Light2
      MP4F A 1 BRIGHT A_Light1
	  MP4F B 1 A_Light0
	  MP4G BC 1
      MP4G B 1 A_Refire
	  MP4G A 3
	  Goto Ready
	Empty:
	  MP4G A 4 A_PlayWeaponSound("919m/dry")
	  Goto Reload 
	Reload:
	  MP4G A 0 A_JumpIfInventory("MP40Mag", 30, "Ready")
	  MP4G A 0 A_JumpIfInventory("TSPPistolAmmo", 1, "ReloadStart")
	  MP4G A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	ReloadStart:
	  MP4G A 0 A_JumpIfInventory("MP40Mag", 1, "ReloadStartFull")
	  MP4R ABCDDDDDD 1
	  MP4R D 0 A_PlaySound("mipfowty/out", 5)
	  MP4R EFGHI 1
	  MP4R I 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
	  MP4R IIIIIIIIIIIIIIIIIIII 1
	  Goto ReloadWork
	ReloadStartFull:
	  MP4C AB 1
	  MP4C C 0 A_PlaySound("mipfowty/bolt1", 5)
	  MP4C DDEEFFGHIJJKKLLMMM 1
	  MP4R ABCDDDDDD 1
	  MP4R D 0 A_PlaySound("mipfowty/out", 5)
	  MP4R EFGHI 1
	  MP4R I 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
	  MP4R IIIIIIIIIIIIIIIIIIII 1
	ReloadWork:
	  MPTR I 0 A_TakeInventory("TSPPistolAmmo",1)
	  MPTR I 0 A_GiveInventory("MP40Mag",1)
	  MPTR I 0 A_JumpIfInventory("MP40Mag",30,"ReloadFinish")
	  MPTR I 0 A_JumpifInventory("TSPPistolAmmo",1,"ReloadWork")
	ReloadFinish:
	  MP4R M 0 A_PlaySound("mipfowty/in", 5)
	  MP4R JKLMNOPPQQRS 1
	  MP4C MMMLLKKJJ 1
	  MP4C J 0 A_PlaySound("mipfowty/bolt2", 5)
	  MP4C IHGFFEEDDCBA 1
	  MP4G A 2
	  Goto Ready
	SPMelee:
	  MP4G A 1
	  MP4M ABCDE 1
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  TNT1 A 0 A_PlaySound("*melee", 6)
	  MP4M FG 1
	  MP4M H 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  MP4M IHGFEDCBA 1
	  MP4G A 6
	  Goto Ready
   }
}

ACTOR MP40Mag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 30
}