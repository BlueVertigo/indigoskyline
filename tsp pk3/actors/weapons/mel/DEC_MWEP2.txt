//===========================================================================
//
// Makoto Arms Type 919m.0
//
// by Marty Kirra
// Sprites by Marty Kirra
//
//===========================================================================

ACTOR Mel919mPistol : Weapon
{
	Weapon.Kickback 110
	Weapon.AmmoType	"AutoPistolMag"
	Weapon.AmmoGive	0
	Weapon.AmmoUse	1
	Weapon.AmmoType2 "TSPPistolAmmo"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2	0
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
	Inventory.PickupMessage "[Makoto Type 919 Mod.0] [Class: Pistol, Slot 2]"
	Obituary "Target %o has been taken out by %k. Assignment Complete."
	+NOAUTOFIRE
	+AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	Tag "Makoto Arms 919M.0"
   States
   {
   Spawn:
	  919P A -1
	  Stop
   Ready:
      TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "ComboIdle")
      MPTG A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoReady")
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      MPTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      LOOP
   AutoReady:
      TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMelee")
      ASTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	  Loop
   ComboIdle:
      TNT1 A 8 A_WeaponReady
	  TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
	  Goto Select
   Deselect:
      TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
   	  TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
      TNT1 A 0 A_PlaySound("919m/deselect", 5)
      TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoDeselect")
	  MPTU DCBA 1
   DeselectLoop:
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
	  TNT1 A 1 A_Lower
      Loop
   AutoDeselect:
      ASTU DCBA 1
   AutoDeselectLoop:
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
	  TNT1 A 1 A_Lower
      Loop
   Select:
	  TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
      TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
	  TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 121)
	  TNT1 A 0 A_PlaySound("919m/select", 5)
	  TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "PistolComboStart")
      TNT1 A 3 SetPlayerProperty(0,1,2)
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoSelect")
      MPTU ABCD 1
   SelectLoop:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
	  MPTG A 1 A_Raise
	  Loop
   AutoSelect:
      ASTU ABCD 1
   AutoSelectLoop:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
	  ASTG A 1 A_Raise
	  Loop
   Fire:
      TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "PistolCombo1")
      TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoFire")
	  TNT1 A 0 A_JumpIfNoAmmo("Empty")
	  MPTF A 0 A_AlertMonsters
	  MPTF A 1 A_GunFlash
	  MPTF B 0 A_PlaySound("919m/fire", 6)
	  MPTF B 0 A_Light2
      MPTF B 1 BRIGHT A_FireBullets(3,2,-1,8,"BulletPuff",1)
	  MPTF C 1 BRIGHT A_Light1
	  MPTG B 1 A_Light0
	  MPTG C 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
	  TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
	  MPTG CDEF 1 A_WeaponReady(WRF_NOBOB)
	  Goto Ready
	AutoFire:
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMelee")
	  TNT1 A 0 A_JumpIfNoAmmo("AutoEmpty")
	  ASTF A 1 BRIGHT
      ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 0.8)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.3,0.3)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
	  TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
	AFire0:
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,9,"919mBulletPuff",1)
      ASTF A 0 A_Jump(64,"AFire3")
	  ASTF A 0 A_Jump(88,"Afire2")
	AFire1:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
	  ASTF B 0 A_Light2
      ASTF B 1 BRIGHT A_Light1
	  ASTF B 0 A_Light0
      ASTF B 0 A_Refire
      Goto FireEnd
	AFire2:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
	  ASTF C 0 A_Light2
      ASTF C 1 BRIGHT A_Light1
	  ASTF C 0 A_Light0
      ASTF C 0 A_Refire
      Goto FireEnd
	AFire3:
	  ASTF B 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
	  ASTF D 0 A_Light2
      ASTF D 1 BRIGHT A_Light1
	  ASTF D 0 A_Light0
      ASTF D 0 A_Refire
	FireEnd:
	  TNT1 A 0
	  ASTG D 1
	  ASTG C 1
	  ASTG B 1
	  Goto Ready
	AltFire:
	  TNT1 A 0 A_PlaySound("919m/autoselect", 5)
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolSingle")
	  ASTS ABCDE 1
	  ASTS F 3
	  ASTS GHIJ 1
	  ASTG A 1
	  ASTS Q 0 A_GiveInventory("AutoPistolFullAuto",1)
	  Goto AutoReady
    AutoPistolSingle:
	  TNT1 A 0 A_PlaySound("919m/autodeselect", 5)
	  ASTS QQQPPONMLKJIHG 1
	  ASTS F 3
	  ASTS EDCBA 1
	  ASTS A 0 A_TakeInventory("AutoPistolFullAuto", 99)
	  ASTS A 1
	  Goto Ready
	AutoEmpty:
	  ASTF A 5 A_PlaySound("919m/dry", 6)
	  Goto AutoPistolReload
	Empty:
	  MPTF A 5 A_PlaySound("919m/dry", 6)
	  Goto Reload
	AutoPistolReload:
	  ASTG A 0 A_JumpIfInventory("AutoPistolMag", 15, "Ready")
	  ASTG A 0 A_JumpIfInventory("TSPPistolAmmo", 1, "APReloadAni")
//	  ASTG A 0 A_JumpIfInventory("AutoPistolMag", 0, "APReloadAni")
	  ASTG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	APReloadAni:
	  ASTG A 1
	  ASTS JI 1
	Reload:
	  MPTG A 0 A_JumpIfInventory("AutoPistolMag", 15, "Ready")
	  MPTG A 0 A_JumpIfInventory("TSPPistolAmmo", 1, "ReloadStart")
	  MPTG A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoEmptyReady")
	  MPTG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	ReloadStart:
	  TNT1 A 0 A_JumpIfInventory("ShotsFiredPewPew", 15, "GiveCockingAnimation")//lewd
	  Goto ReloadCont
	GiveCockingAnimation:  
	  TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
	ReloadCont:
	  TNT1 A 0 A_TakeInventory("ShotsFiredPewPew", 99)
      MPTG BCD 1
	  MPTG D 0 A_FireCustomMissile("SpentClip1",3,0,-4,-12)
	  MPTR A 1 A_PlaySound("919m/magout", 5)
	  MPTR BCD 1
	  MPTR EEEEEEEEEEEEEEEEEEEE 1
	  TNT1 A 0 A_PlaySound("919m/magin1", 6)
	  MPTR FGH 1
	  MPTR I 1
	ReloadWork:
	  MPTR I 0 A_TakeInventory("TSPPistolAmmo",1)
	  MPTR I 0 A_GiveInventory("AutoPistolMag",1)
	  MPTR I 0 A_JumpIfInventory("AutoPistolMag",15,"ReloadFinish")
	  MPTR I 0 A_JumpifInventory("TSPPistolAmmo",1,"ReloadWork")
	ReloadFinish:
	  MPTR JKLMNNNNOP 1
	  MPTR Q 1 A_PlaySound("919m/magin2", 5)
	  MPTR QRSTUVWX 1
	  MPTG A 3
	  TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolFinish")
	  Goto Ready
	AutoPistolFinish:
	  ASTS IJ 1
	  Goto AutoReady
	AutoEmptyReady:
	  ASTG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto AutoReady
	Rechamber:
	  MPTC A 0 A_PlaySound("919m/cock", 6)
	  MPTC ABCC 1
	  MPTC DEFGHIJ 1
	  MPTG A 2
	  TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolFinish")
	  Goto Ready
	PistolComboStart:
	  TNT1 A 0 A_Raise
	  TNT1 A 0 A_JumpIfInventory("ShotsFiredPewPew", 11, "Select")
	  TNT1 AAAAAAAAAA 1 A_WeaponReady
	  TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
	  Goto Select
	PistolCombo1:
	  TNT1 A 0 A_JumpIfInventory("ShotsFiredPewPew", 11, "AfterCombo")
	  TNT1 A 0 A_TakeInventory("AutoPistolFullAuto", 99)
	  TNT1 A 0 A_GiveInventory("AutoPistolFullAuto", 1)
	  TNT1 A 3 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
	  MPCF ABCD 1
	  ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 1.6)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,7,"BulletPuff",1)
      MPCF EF 1
	  ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 1.6)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,7,"BulletPuff",1)
	  MPCF GH 1
	  ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 1.6)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,7,"BulletPuff",1)
	  MPCF IJ 1
	  ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 1.6)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,7,"BulletPuff",1)
	  MPCF KL 1
	  ASTG A 0 A_GunFlash
	  ASTG A 0 A_AlertMonsters
	  MPTG A 0 A_SetPitch(pitch - 1.6)
	  MPTG A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
      ASTG A 0 A_PlaySound("919m/fire", 5)
      ASTF A 0 BRIGHT A_FireBullets(6,6,-1,7,"BulletPuff",1)
	  MPCF MN 1
	  TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 5)
	  TNT1 A 8
	AfterCombo:
	  TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
	  ASTU ABCD 1
	  Goto Ready
//Melee Start
	SPMelee:
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoModeMelee")
	  MPTD ABCD 1
	SPMeleeStart:
	  TNT1 A 0 A_Jump(128,"Melee2")
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  MPTM ABCDE 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
	  MPTM F 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeBerserk:
	  MPTM F 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeA: 	  
	  MPTM GHI 1
	  TNT1 A 6
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
	  MPTS ABCD 1
	  Goto Ready
	Melee2:
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  MPTM JKLM 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"Melee2Berserk")
	  MPTM N 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2Berserk:
	  MPTM N 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2A: 
	  MPTM OPQR 1
	  TNT1 A 6
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
	  MPTS ABCD 1
	  Goto Ready
//Auto Mode Melee
	AutoModeMelee:
	  ASTD ABCD 1
	AutoModeMeleeStart:
	  TNT1 A 0 A_Jump(256,"SPMeleeStart")
	AutoMeleeCooldown:
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMeleeStart")
	  ASTU EFGH 1
	  Goto AutoReady
   }
}

ACTOR AutoPistolMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 15
}

ACTOR AutoPistolFullAuto : Inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 1
}

ACTOR ShotsFiredPewPew : Inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 15
}

ACTOR NeedstheCock : Inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 1
}

//===========================================================================
//
// Makoto Arms Type 919m.0 (Magazine Version)
// Made for the Lone Survivor gameplay type
//
// by Marty Kirra
// Sprites by Marty Kirra
//
//===========================================================================

ACTOR Mel919mPistolLS : Mel919mPistol
{
	Weapon.AmmoType2 "TSPPistolAmmoMag"
	+WEAPON.CHEATNOTWEAPON
   States
   {
   Spawn:
	  919P A -1
	  Stop
   Ready:
      TNT1 A 0 A_JumpIfInventory("919mUpgrade", 1, "UpgradeReady")
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      MPTG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
      LOOP
   UpgradeReady:
      TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "ComboIdle")
      MPTG A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoReady")
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      MPTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      LOOP
    Fire:
      TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "PistolCombo1")
      TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoFire")
	  TNT1 A 0 A_JumpIfNoAmmo("Empty")
	  MPTF A 0 A_AlertMonsters
	  MPTF A 1 A_GunFlash
	  MPTF B 0 A_PlaySound("919m/fire", 6)
	  MPTF B 0 A_Light2
      MPTF B 1 BRIGHT A_FireBullets(3,2,-1,8,"BulletPuff",1)
	  MPTF C 1 BRIGHT A_Light1
	  MPTG B 1 A_Light0
	  MPTG C 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
	  TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
	FireFinish:
	  TNT1 A 0 A_JumpIfInventory("919mUpgrade", 1, "FireFinishUpgrade")
	  MPTG CDEF 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
	  Goto Ready
	FireFinishUpgrade:
	  MPTG CDEF 1 A_WeaponReady(WRF_NOBOB)
	  Goto Ready
	AltFire:
	  TNT1 A 0 A_PlaySound("919m/autoselect", 5)
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolSingle")
	  ASTS ABCDE 1
	  ASTS F 3
	  ASTS GHIJ 1
	  ASTG A 1
	  ASTS Q 0 A_GiveInventory("AutoPistolFullAuto",1)
	  Goto AutoReady
    AutoPistolSingle:
	  TNT1 A 0 A_PlaySound("919m/autodeselect", 5)
	  ASTS QQQPPONMLKJIHG 1
	  ASTS F 3
	  ASTS EDCBA 1
	  ASTS A 0 A_TakeInventory("AutoPistolFullAuto", 99)
	  ASTS A 1
	  Goto Ready
	AutoEmpty:
	  ASTF A 5 A_PlaySound("919m/dry", 6)
	  Goto AutoPistolReload
	Empty:
	  MPTF A 5 A_PlaySound("919m/dry", 6)
	  Goto Reload
	AutoPistolReload:
	  ASTG A 0 A_JumpIfInventory("AutoPistolMag", 15, "Ready")
	  ASTG A 0 A_JumpIfInventory("TSPPistolAmmoMag", 1, "APReloadAni")
	  ASTG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	APReloadAni:
	  ASTG A 1
	  ASTS JI 1
	Reload:
	  MPTG A 0 A_JumpIfInventory("919MUpgrade", 1, "UpgradeReload")
	  MPTG A 0 A_JumpIfInventory("AutoPistolMag", 15, "Ready")
	  MPTG A 0 A_JumpIfInventory("TSPPistolAmmoMag", 1, "ReloadStart")
	  MPTG A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoEmptyReady")
	  MPTG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOFIRE)
	  Goto Ready
	UpgradeReload:
	  MPTG A 0 A_JumpIfInventory("AutoPistolMag", 15, "Ready")
	  MPTG A 0 A_JumpIfInventory("TSPPistolAmmoMag", 1, "ReloadStart")
	  MPTG A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoEmptyReady")
	  MPTG A 1 A_WeaponReady(WRF_NOFIRE)
	  Goto Ready
	ReloadStart:
	  TNT1 A 0 A_JumpIfInventory("ShotsFiredPewPew", 15, "GiveCockingAnimation")//lewd
	  Goto ReloadCont
	GiveCockingAnimation:  
	  TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
	ReloadCont:
	  TNT1 A 0 A_TakeInventory("ShotsFiredPewPew", 99)
      MPTG BCD 1
      TNT1 A 0 A_TakeInventory("AutoPistolMag",15)
	  MPTG D 0 A_FireCustomMissile("SpentClip1",3,0,-4,-12)
	  MPTR A 1 A_PlaySound("919m/magout", 5)
	  MPTR BCD 1
	  MPTR EEEEEEEEEEEEEEEEEEEE 1
	  TNT1 A 0 A_PlaySound("919m/magin1", 6)
	  MPTR FGH 1
	  MPTR I 1
	ReloadWork:
	  MPTR I 0 A_TakeInventory("TSPPistolAmmoMag",1)
	  MPTR I 0 A_GiveInventory("AutoPistolMag",15)
	  Goto ReloadFinish
	ReloadFinish:
	  MPTR JKLMNNNNOP 1
	  MPTR Q 1 A_PlaySound("919m/magin2", 5)
	  MPTR QRSTUVWX 1
	  MPTG A 3
	  TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolFinish")
	  Goto Ready
	Rechamber:
	  MPTC A 0 A_PlaySound("919m/cock", 6)
	  MPTC ABCC 1
	  MPTC DEFGHIJ 1
	  MPTG A 2
	  TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto", 1, "AutoPistolFinish")
	  Goto Ready
	SPMelee:
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoModeMelee")
	  MPTD ABCD 1
	SPMeleeStart:
	  TNT1 A 0 A_Jump(128,"Melee2")
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  MPTM ABCDE 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
	  MPTM F 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeBerserk:
	  MPTM F 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeA: 	  
	  MPTM GHI 1
	  TNT1 A 14
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
	  MPTS ABCD 1
	  Goto Ready
	Melee2:
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  MPTM JKLM 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"Melee2Berserk")
	  MPTM N 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2Berserk:
	  MPTM N 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2A: 
	  MPTM OPQR 1
	  TNT1 A 14
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
	  MPTS ABCD 1
	  Goto Ready
//Auto Mode Melee
	AutoModeMelee:
	  ASTD ABCD 1
	AutoModeMeleeStart:
	  TNT1 A 0 A_Jump(256,"SPMeleeStart")
	AutoMeleeCooldown:
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMeleeStart")
	  ASTU EFGH 1
	  Goto AutoReady
   }
}

//===========================================================================
//
// Makoto Arms Type 919m.0
//
// by Marty Kirra
// Sprites by Marty Kirra
//
//===========================================================================

ACTOR Mel919mPistolGold : Weapon
{
	Weapon.Kickback 110
	Weapon.AmmoType	"GoldAutoPistolMag"
	Weapon.AmmoGive	0
	Weapon.AmmoUse	1
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
	Inventory.PickupMessage "[Makoto Type 919 Mod.0 (Gold)] [Class: Pistol, Slot 2]"
	Obituary "Target %o has been taken out by %k. Assignment Complete."
	+NOAUTOFIRE
	+AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	Tag "Makoto Arms 919M.0 (Gold)"
   States
   {
   Spawn:
	  919P A -1
	  Stop
   Ready:
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
      GPSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      LOOP
   Deselect:
      TNT1 A 0 A_PlaySound("919m/deselect", 5)
	  GPSU DCBA 1
   DeselectLoop:
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
	  TNT1 A 1 A_Lower
      Loop
   Select:
	  TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
      TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
	  TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 121)
	  TNT1 A 0 A_PlaySound("919m/select", 5)
      TNT1 A 3 SetPlayerProperty(0,1,2)
      GPSU ABCD 1
   SelectLoop:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
	  GPSG A 1 A_Raise
	  Loop
   Fire:
	  TNT1 A 0 A_JumpIfNoAmmo("Empty")
	  GPSF A 0 A_AlertMonsters
	  GPSF A 1 A_GunFlash
	  GPSF B 0 A_PlaySound("919m/goldfire", 6)
	  GPSF B 0 A_Light2
      GPSF B 1 BRIGHT A_FireBullets(3,2,-1,14,"BulletPuff",1)
	  GPSF C 1 BRIGHT A_Light1
	  GPSG B 1 A_Light0
	  GPSG C 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
	  TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
	  GPSG CDEF 1 A_WeaponReady(WRF_NOBOB)
	  Goto Ready
	Empty:
	  GPSF A 5 A_PlaySound("919m/dry", 6)
	  Goto Reload
	Reload:
	  TNT1 A 0 A_JumpIfInventory("GoldAutoPistolMag", 15, "Ready")
	  TNT1 A 0 A_JumpIfInventory("ShotsFiredPewPew", 15, "GiveCockingAnimation")//lewd
	  Goto ReloadCont
	GiveCockingAnimation:  
	  TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
	ReloadCont:
	  TNT1 A 0 A_TakeInventory("ShotsFiredPewPew", 99)
      GPSG BCD 1
	  GPSG D 0 A_FireCustomMissile("SpentClip1",3,0,-4,-12)
	  GPSR A 1 A_PlaySound("919m/magout", 5)
	  GPSR BCD 1
	  GPSR EEEEEEEEEEEEEEEEEEEE 1
	  TNT1 A 0 A_PlaySound("919m/magin1", 6)
	  GPSR FGH 1
	  GPSR I 1
	ReloadWork:
	  GPSR I 0 A_GiveInventory("GoldAutoPistolMag",1)
	  GPSR I 0 A_JumpIfInventory("GoldAutoPistolMag",15,"ReloadFinish")
	  Goto ReloadWork
	ReloadFinish:
	  GPSR JKLMNNNNOP 1
	  GPSR Q 1 A_PlaySound("919m/magin2", 5)
	  GPSR QRSTUVWX 1
	  GPSG A 3
	  TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
	  Goto Ready
	Rechamber:
	  GPSC A 0 A_PlaySound("919m/cock", 6)
	  GPSC ABCC 1
	  GPSC DEFGHIJ 1
	  GPSG A 2
	  TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
	  Goto Ready
//Melee Start
	SPMelee:
	  GPSD ABCD 1
	SPMeleeStart:
	  TNT1 A 0 A_Jump(128,"Melee2")
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  GPSM ABCDE 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
	  GPSM F 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeBerserk:
	  GPSM F 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto SPMeleeA
	SPMeleeA: 	  
	  GPSM GHI 1
	  TNT1 A 6
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  GPSS ABCD 1
	  Goto Ready
	Melee2:
	  TNT1 A 7
	  TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
	  GPSM JKLM 1
	  ML1F D 0 A_JumpIfInventory("PowerStrength",1,"Melee2Berserk")
	  GPSM N 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2Berserk:
	  GPSM N 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
	  Goto Melee2A
	Melee2A: 
	  GPSM OPQR 1
	  TNT1 A 6
	  TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
	  GPSS ABCD 1
	  Goto Ready
   }
}

ACTOR GoldAutoPistolMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 15
}

ACTOR 919mBulletPuff : BulletPuff
{
    Decal BulletChip
    +NOEXTREMEDEATH
}